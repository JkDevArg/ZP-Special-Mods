/*
	[ZPSp] Gamemode: Deathmatch Mode

	* Description:
		Its Like swarm mode but you can respawn

	* Cvars:
		zp_deathmatch_minplayers "2" - Min Players for start a mode
		zp_deathmatch_inf_ratio "0.5" - Ratio of gamemode

*/

#include <amxmodx>
#include <fun>
#include <zombie_plague_special>
#include <amx_settings_api>

#if ZPS_INC_VERSION < 43
	#assert Zombie Plague Special 4.3 Include File Required. Download Link: https://forums.alliedmods.net/showthread.php?t=260845
#endif

new const ZP_CUSTOMIZATION_FILE[] = "zombie_plague_special.ini"

new Array:g_sound_deathmatch, g_ambience_sounds, Array:g_sound_amb_dm_duration, Array: g_sound_amb_dm

// Default Sounds
new const sound_deathmatch[][] = { "ambience/the_horror2.wav" }
new const ambience_deathmatch_sound[][] = { "zombie_plague/ambience.wav" } 
new const ambience_deathmatch_dur[][] = { "17" }

// Variables
new g_gameid, g_maxplayers, cvar_minplayers, cvar_ratio, g_msg_sync

new const g_chance = 80

#if ZPS_INC_VERSION >= 44
new const gm_respawn_limit = 3 // Respawn Limit per Player (Zombie Plague Special 4.4 Version or higher requires)
#endif

// Ambience sounds task
#define TASK_AMB 3256

// Enable Ambience?
#define AMBIENCE_ENABLE 0

public plugin_init()
{
	// Plugin registeration.
	register_plugin("[ZP] Deathmatch Mode","1.0", "@bdul! | [P]erfec[T] [S]cr[@]s[H]")
	
	// Register some cvars
	cvar_minplayers = register_cvar("zp_deathmatch_minplayers", "2")
	cvar_ratio = register_cvar("zp_deathmatch_inf_ratio", "0.5")
	
	// Get maxplayers
	g_maxplayers = get_maxplayers()
	
	// Hud stuff
	g_msg_sync = CreateHudSyncObj()
}

public plugin_natives() {
	register_native("zp_is_deathmatch_round", "native_is_deathmatch_round", 1)
}

// Game modes MUST be registered in plugin precache ONLY
public plugin_precache()
{
	// Read the access flag
	new user_access[40], i
	if(!amx_load_setting_string(ZP_CUSTOMIZATION_FILE, "Access Flags", "START MODE DEATHMATCH", user_access, charsmax(user_access))) {
		amx_save_setting_string(ZP_CUSTOMIZATION_FILE, "Access Flags", "START MODE DEATHMATCH", "a")
		formatex(user_access, charsmax(user_access), "a")
	}
	
	new access_flag = read_flags(user_access)
	
	g_sound_deathmatch = ArrayCreate(64, 1)
	g_sound_amb_dm = ArrayCreate(64, 1)
	g_sound_amb_dm_duration = ArrayCreate(64, 1)
	
	// Load from external file
	amx_load_setting_string_arr(ZP_CUSTOMIZATION_FILE, "Sounds", "ROUND DEATHMATCH", g_sound_deathmatch)
	
	// Precache the play sounds
	if (ArraySize(g_sound_deathmatch) == 0)
	{
		for (i = 0; i < sizeof sound_deathmatch; i++)
			ArrayPushString(g_sound_deathmatch, sound_deathmatch[i])
		
		// Save to external file
		amx_save_setting_string_arr(ZP_CUSTOMIZATION_FILE, "Sounds", "ROUND DEATHMATCH", g_sound_deathmatch)
	}
	
	// Precache sounds
	static sound[100]
	for (i = 0; i < ArraySize(g_sound_deathmatch); i++)
	{
		ArrayGetString(g_sound_deathmatch, i, sound, charsmax(sound))
		precache_ambience(sound)
	}
	
	// Ambience Sounds
	g_ambience_sounds = AMBIENCE_ENABLE
	if(!amx_load_setting_int(ZP_CUSTOMIZATION_FILE, "Ambience Sounds", "DEATHMATCH ENABLE", g_ambience_sounds))
		amx_save_setting_int(ZP_CUSTOMIZATION_FILE, "Ambience Sounds", "DEATHMATCH ENABLE", g_ambience_sounds)
	
	amx_load_setting_string_arr(ZP_CUSTOMIZATION_FILE, "Ambience Sounds", "DEATHMATCH SOUNDS", g_sound_amb_dm)
	amx_load_setting_string_arr(ZP_CUSTOMIZATION_FILE, "Ambience Sounds", "DEATHMATCH DURATIONS", g_sound_amb_dm_duration)
	
	
	// Save to external file
	if (ArraySize(g_sound_amb_dm) == 0) {
		for (i = 0; i < sizeof ambience_deathmatch_sound; i++)
			ArrayPushString(g_sound_amb_dm, ambience_deathmatch_sound[i])
		
		amx_save_setting_string_arr(ZP_CUSTOMIZATION_FILE, "Ambience Sounds", "DEATHMATCH SOUNDS", g_sound_amb_dm)
	}
	
	if (ArraySize(g_sound_amb_dm_duration) == 0) {
		for (i = 0; i < sizeof ambience_deathmatch_dur; i++)
			ArrayPushString(g_sound_amb_dm_duration, ambience_deathmatch_dur[i])
		
		amx_save_setting_string_arr(ZP_CUSTOMIZATION_FILE, "Ambience Sounds", "DEATHMATCH DURATIONS", g_sound_amb_dm_duration)
	}
	
	// Ambience Sounds
	static buffer[250]
	if (g_ambience_sounds) {
		for (i = 0; i < ArraySize(g_sound_amb_dm); i++) {
			ArrayGetString(g_sound_amb_dm, i, buffer, charsmax(buffer))
			precache_ambience(buffer)
		}
	}
	
	// Register our game mode
	#if ZPS_INC_VERSION < 44
	g_gameid = zp_register_game_mode("Deathmatch", access_flag, g_chance, 0, ZP_DM_BALANCE)
	#else
	g_gameid = zpsp_register_gamemode("Deathmatch", access_flag, g_chance, 0, ZP_DM_BALANCE, gm_respawn_limit)
	#endif
}

public zp_round_started_pre(game)
{
	// Check if it is our game mode
	if(game == g_gameid)
	{
		// Check for min players
		if(zp_get_alive_players() < get_pcvar_num(cvar_minplayers))
			return ZP_PLUGIN_HANDLED

		// Start our new mode
		start_deathmatch_mode()
	}
	// Make the compiler happy =)
	return PLUGIN_CONTINUE
}

public zp_round_started(game, id)
{
	// Check if it is our game mode
	if(game == g_gameid)
	{
		// Show HUD notice
		set_hudmessage(221, 156, 21, -1.0, 0.17, 1, 0.0, 5.0, 1.0, 1.0, -1)
		ShowSyncHudMsg(0, g_msg_sync, "Deathmatch Mode !!!")
		
		// Play the starting sound
		static sound[100]
		ArrayGetString(g_sound_deathmatch, random_num(0, ArraySize(g_sound_deathmatch) - 1), sound, charsmax(sound))
		
		#if ZPS_INC_VERSION < 44
		PlaySoundToClients(sound)
		#else
		zp_play_sound(0, sound)
		#endif
		
		// Remove ambience task affects
		remove_task(TASK_AMB)
		
		// Set task to start ambience sounds
		set_task(2.0, "start_ambience_sounds", TASK_AMB)
	}
}

public zp_game_mode_selected(gameid, id)
{
	// Check if our game mode was called
	if(gameid == g_gameid)
		start_deathmatch_mode()
	
	// Make the compiler happy again =)
	return PLUGIN_CONTINUE
}

// This function contains the whole code behind this game mode
start_deathmatch_mode()
{
	// Create and initialize some important vars
	static i_zombies, i_max_zombies, id, i_alive
	i_alive = zp_get_alive_players()
	id = 0
	
	// Get the no of players we have to turn into zombies
	i_max_zombies = floatround((i_alive * get_pcvar_float(cvar_ratio)), floatround_ceil)
	i_zombies = 0
	
	// Randomly turn players into zombies
	while (i_zombies < i_max_zombies)
	{
		// Keep looping through all players
		if ((++id) > g_maxplayers) id = 1
		
		// Dead
		if (!is_user_alive(id))
			continue;
		
		// Random chance
		if (random_num(1, 5) == 1) {
			// Make user zombie
			zp_infect_user(id)

			if(zp_is_escape_map())
				zp_do_random_spawn(id)
			
			// Increase counter
			i_zombies++
		}
	}
}

public start_ambience_sounds()
{
	if (!g_ambience_sounds)
		return;
	
	// Variables
	static amb_sound[64], sound,  str_dur[20]
	
	// Select our ambience sound
	sound = random_num(0, ArraySize(g_sound_amb_dm)-1)

	ArrayGetString(g_sound_amb_dm, sound, amb_sound, charsmax(amb_sound))
	ArrayGetString(g_sound_amb_dm_duration, sound, str_dur, charsmax(str_dur))
	
	#if ZPS_INC_VERSION < 44
	PlaySoundToClients(amb_sound)
	#else
	zp_play_sound(0, amb_sound)
	#endif
	
	// Start the ambience sounds
	set_task(str_to_float(str_dur), "start_ambience_sounds", TASK_AMB)
}
public zp_round_ended() {
	// Stop ambience sounds on round end
	remove_task(TASK_AMB)
}

public native_is_deathmatch_round() {
	return (zp_get_current_mode() == g_gameid)
}

precache_ambience(sound[])
{
	static buffer[150]
	if(equal(sound[strlen(sound)-4], ".mp3")) {
		if(!equal(sound, "sound/", 6) && !file_exists(sound) && !equal(sound, "media/", 6))
			format(buffer, charsmax(buffer), "sound/%s", sound)
		else
			format(buffer, charsmax(buffer), "%s", sound)
		
		precache_generic(buffer)
	}
	else  {
		if(equal(sound, "sound/", 6))
			format(buffer, charsmax(buffer), "%s", sound[6])
		else
			format(buffer, charsmax(buffer), "%s", sound)
		
		
		precache_sound(buffer)
	}
}

#if ZPS_INC_VERSION < 44
// Plays a sound on clients
stock PlaySoundToClients(const sound[])
{
	static buffer[150]

	if(equal(sound[strlen(sound)-4], ".mp3")) {
		if(!equal(sound, "sound/", 6) && !file_exists(sound) && !equal(sound, "media/", 6))
			format(buffer, charsmax(buffer), "sound/%s", sound)
		else
			format(buffer, charsmax(buffer), "%s", sound)
	
		client_cmd(0, "mp3 play ^"%s^"", buffer)

	}
	else {
		if(equal(sound, "sound/", 6))
			format(buffer, charsmax(buffer), "%s", sound[6])
		else
			format(buffer, charsmax(buffer), "%s", sound)
			
		client_cmd(0, "spk ^"%s^"", buffer)
	}
}
#endif
